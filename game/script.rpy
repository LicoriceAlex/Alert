define vector = Character('Вектор', color="#cd03c9")
define billy = Character('Билли', color="#ff8b06")
define storyteller = Character('Рассказчик', color="#6453ff")
define aatos = Character('Аатос', color='#00a103')

label start:
    play music ingame_music
    scene elite_building_2_version
    pause
    
    scene hero_monit with dissolve
    storyteller "Вектор был отчислен из института IT-технологий из-за того, что весь 2-ой курс даже не появлялся на парах"

    scene funeral with dissolve
    storyteller "Все эти прогулы были связаны со смертью его матери от рака желудка"

    scene door with dissolve
    storyteller "Впавший в глубочайшую депрессию, Вектор закрывается в себе"

    scene father with dissolve
    storyteller "Его отцом был влиятельный бизнесмен, которому не было дела до воспитания сына (от первого и неудачного брака), в отличии от умершей матери"
    
    scene pcs with dissolve
    storyteller "Спустя год депрессии после трагедии, Вектор, благодаря деньгам отца, смог обеспечить себя лучшим хакерским оборудованием"

    scene hello_mayhem
    storyteller "Также ему удалось найти в даркнете группировку “Hello, mayhem”, крайне радикально настроенных чёрных хакеров, стремящихся разрушить мировой порядок и явить миру своё Я"

    scene education
    storyteller "На протяжении 2 лет они обучали Вектора всему, что умели сами"

    scene some_person
    storyteller "Наконец лидеры движения дали Вектору первое задание: “Отомстить главному обидчику”, после чего Вектор наконец покажет, что он достоин для посвящения в великий план “Hello, mayhem”"

    scene city with dissolve
    storyteller "Про этот план он слышит каждый день, но до сих пор ничего не знает..."

    scene room with Fade(0.5, 1.0, 0.5)
    storyteller "Вектор долго думает, что же сподвигло его встать на такой путь"
    storyteller "Краем глаза он замечает на тумбочке фотографию покойной мамы"

    scene room_mom
    show vector at right
    vector "Я ненавижу своего отца, ему всегда была безразлична наша семья и мои проблемы"
    hide vector

    scene working with dissolve
    storyteller "В порыве ярости Вектор хватает свой ноутбук. Чем больше он нажимает на клавиши клавиатуры, тем глубже он проникает в глубокий интернет в поисках наказания, которое понесёт его отец за несчастную жизнь, полную депрессий и печали после смерти матери Вектора"

    scene transition with dissolve
    storyteller "Несколько дней спустя..."

    play music before_the_night
    scene night_building with fade
    storyteller "Эта компания является лидером на мировом рынке в области информационной безопасности, которой владеет отец Вектора"

    scene office
    show billy at right 
    storyteller "Мы видим молодого парня со шрамом на правом виске. Он сидит на своём рабочем месте, свет от ноутбука падает ему на лицо"
    hide billy

    scene winner
    storyteller "Недавно Билли смог победить в конкурсе среди 10000+ айтишников со всей страны"

    scene office
    storyteller "Теперь он получил возможность стажироваться в одном из зданий такого гиганта на рынке, как Arasaka."

    show billy at right
    storyteller "Наш герой почти дописал код, который будет надёжно защищать одну частную организацию от DDoS атак. Помоги ему быстрее справиться с поставленной задачей"
    hide billy

    call quiz from _call_quiz

    scene watch
    storyteller "Билли смотрит на часы"

    scene office
    storyteller "Погружённый в свою работу, Билли даже не заметил как офис опустел"
    
    scene coridor_janitor
    storyteller "Где-то вдалеке слышно лишь уборщика"

    play sound alert
    scene office alert
    storyteller "Внезапно все кабинеты заливаются красным светом и раздаётся звук сирены"

    scene office alert on window
    storyteller "Одновременно на всех экранах мониторов вспыхивает надпись “ALERT”"
    stop sound

    scene office
    storyteller "Почти тут же всё смолкает"

    play music before_the_night

    show billy at left
    billy "Что это было? Не прошло и 10 секунд как сирена смолкла и офисы обрели привычные им оттенки. Мне нужно срочно сбегать в серверную, дабы выяснить в чём дело."
    billy "Что же мне сделать первым делом? (Билли берёт свой ноутбук)"
    hide billy

    menu:
        storyteller "Билли стоит в огромном офисе и перед ним открывается выбор"
        "Поговорить с уборщиком":
            call janitor from _call_janitor
            menu:
                "Подойти к портрету директора компании и немного поразмышлять":
                    call director_portrait from _call_director_portrait
                    menu:
                        "Бежать в серверную":
                            jump server_room
                "Бежать в серверную":
                    jump server_room
        "Подойти к портрету директора компании и немного поразмышлять":
            call director_portrait from _call_director_portrait_1
            menu:
                "Поговорить с уборщиком":
                    call janitor from _call_janitor_1
                    menu:
                        "Бежать в серверную":
                            jump server_room
                "Бежать в серверную":
                    jump server_room
        "Бежать в серверную":
            jump server_room


label janitor:
    scene coridor_janitor
    stop music fadeout 4.0
    play sound aatos_song fadein 1.0
    storyteller "Билли идёт по тёмному коридору постепенно приближаясь к уборщику (пока он к нему идёт, он слышит как уборщик что-то напевает себе под нос)"
    scene aatos_bage
    storyteller "Билли подходит к уборщику и обращает внимание на бейджик пожилого человека, на котором написано Аатос"
    scene corridor_empty
    show billy at left
    billy "Здравствуйте, Вы не знаете, что слу...??"
    storyteller "Не дав договорить Билли, Аатос перебивает его"
    stop sound fadeout 1.5
    play music before_the_night fadein 2.0 volume 0.5
    show aatos at right
    aatos "А, это ты! Я ждал тебя. Мне как раз нужен помощник, чтобы разобраться во всём этом paska"
    menu:
        "Стойте, мы разве знакомы?":
            billy "Стойте, мы разве знакомы?"
            aatos "А как же. Я постоянно наблюдаю за вами"
            billy "Простите, что?"
            aatos "На днях я узнал пароль от кое-чего интересного."
            billy "Зачем вы мне это рассказываете?"
            aatos "Мне не нужен помощник, который постоянно задаёт вопросы. Катись отсюда! Не мешай мне работать!"
            billy "(про себя)Чокнутый"
            aatos "Ты что-то там вякнул сопляк?"
            billy "Нет. Лучше оставлю вас в покое"

        "И чем же я могу вам помочь?":
            billy "И чем же я могу вам помочь?"
            aatos  "У нас в столовой завелись муравьи. Начальство попросило выгнать их. Они могут узнать то, что им не следует."
            billy "В здании сработала сигнализация, я вы думаете о каких-то муравьях?"
            aatos "Оооо, Nuorukainen, это не просто муравьи. Они живые"
            billy "(про себя)Чокнутый"
            storyteller "Уходит подальше от пожилого мужчины"
    scene coridor_janitor
    stop music fadeout 0.5
    play sound aatos_song
    storyteller "Уборщик возобновляет своё пение"
    stop sound
    scene office
    return


label director_portrait:
    scene portrait
    storyteller "Билли подходит к портрету Основателя и сверлит его взглядом"
    storyteller "Мне знаком этот взгляд. Билли охватывает гордость, что он смог пробиться в такую престижную компания, но одновременно он чувствует некую грусть, которую сам не может объяснить"
    billy "Интересно, смогу ли я когда-нибудь занять его место?"
    scene office
    return


label server_room:
    scene coridor
    storyteller "Билли выходит в коридор"
    storyteller "Билли бежит по коридору в серверную по дорогу чуть не споткнувшись"

    scene entering_server_room
    storyteller "Билли подбегает к двери в серверную"
    storyteller "Достаёт из кармана карту доступа и вставляет её в защищённую дверь"

    scene billy_in_server_room
    play music sinoia_main_theme
    storyteller "Перед ним открывается огромное помещение"
    storyteller "Билли стоит перед огромной комнатой"
    storyteller "Как же он впивается глазами в каждое оборудование в поисках каких-либо аномалий"

    scene red_bulb
    storyteller "На одной из стоек с серверами он замечает мигающую красную лампочку и бежит к ней"
    storyteller "Билли с помощью своего ноутбука подключается к данному серверу"

    scene laptop_server_room
    storyteller "И тут его пальцы начинают скользить по клавиатуре ноутбука, который он подключил к аномальному серверу"
    storyteller "Расшифровав строки кода Билли наконец понимает в чём дело. Тревога выключилась не просто так, что-то подавляет сигнал дистанционно"
    show billy at left
    billy "Весьма необычно. Но надо понять, что же вызвало тревогу"
    hide billy

    scene server_room_working
    storyteller "Билли ещё ближе уставился в экран и энергичней нажимает на клавиши"
    storyteller "Билли вновь углубляется в цифровую вселенную , состоящую из строчек кодов"
    storyteller "Билли всё также погружён в свои мысли и экран ноутбука"

    scene eye
    storyteller "Спустя 5 минут непрерывной работы Билли находит причину пугающей надписи “Alert”"
    scene arasaka rocket
    storyteller "В головное здание Арасаки летит ракета"
    storyteller "Расчёты показывают, что до поражения выбранной цели остаётся каких-то 7 минут"
    scene server_room_shocked_working
    storyteller "Билли понимает, если Арасака падёт, то конфиденциальные данные миллионов людей окажутся под угрозой взлома со стороны хакерских группировок"
    scene stealing_money
    storyteller "Как говорят взломщики: “Нет защиты - нет проблем”"
    scene server_room_working 
    menu:
        storyteller "Потрясённый, Билли размышляет, что он может сделать"
        "Рискнуть и попробовать деактивировать ракету":
            jump deactivation
        "Попробовать выяснить, кто дал одобрение на запуск ракеты (быть может это поможет из обезвредить)":
            jump figure_out


label figure_out:
    play music rite_of_passage
    storyteller "Билли ныряет с головой в исходный код в поисках малейшей зацепки"
    storyteller "Наконец на экране появляется имя отправителя “Кукушонок” с припиской ‘Ещё не понял кто я?“"
    scene server_room_shocked_working
    storyteller "У Билли перехватывает дыхание, перед ним проносятся шлешбэки из детства, в которых его мама всегда называла Кукушонком, своего любимого и единственного сына"
    storyteller "Настоящее, будущее и прошлое, всё перемешивается в голове у Билли. Еле как он собирается с мыслями и смотрит на таймер"
    show billy at left
    billy "Мне не хватит времени на деактивацию ракеты"
    hide billy
    menu:
        storyteller "Билли вновь устремляется в свой ноутбук. Наконец он успевает найти решение: можно изменить координаты ракеты, но только на другое здание Арасаки. Это здание в котором он сейчас и находится"
        "Оставить всё так как оно и есть":
            show billy at left
            billy "Я ещё пожалею об этом решении"
            hide billy
            scene arasaka_explosion
            storyteller "Арасака взрывается"
            storyteller "Конец..."
            return
        "Перенаправить ракету и взорвать здание в котором находишься":
            show billy at left
            billy "Я уже не успею эвакуироваться, зато спасу миллионы людей"
            hide billy
            scene building_explosion
            storyteller "Здание взрывается"
            storyteller "Конец..."
            return
        "Разобраться и понять, как он мог быть Вектором":
            scene personality_split
            menu:
                storyteller "И тут между Билли и Вектором вспыхивает внутренний разговор. Билли пытается сосредоточиться хоть на чём-то, но у антагониста есть цель - разрушение"
                "Опустить руки":
                    show billy at left
                    billy "У меня нет сил со всем этим разбираться. Я так устал!"
                    hide billy
                    scene arasaka_explosion
                    storyteller "Арасака взрывается"
                    storyteller "Конец..."
                    return
                "Перенаправить ракету на здание в котором сейчас нахожусь":
                    show billy at left
                    show vector at right
                    billy "Раз я тебя породил, так и умрём мы вместе"
                    hide billy
                    scene building_explosion
                    storyteller "Здание взрывается"
                    storyteller "Конец..."
                    return
                "Поддаться Вектору":
                    show vector at right
                    vector "Есть запасной код, который перенаправит ракету на дом нашего отца, ведь именно из-за него, из-за отсутствия поддержки после смерти мамы, я стал чёрным хакером и террористом"
                    storyteller "О нееет! Похоже, что слова Вектора смогли зацепить нашего героя"
                    hide vector
                    show billy at left
                    menu:
                        billy "В чём-то он прав. Как мне быть?"
                        "Отказаться от плана Вектора":
                            billy "Ты сумасшедший, я люблю своего отца. Это благодаря ему, я смог…"
                            hide billy
                            scene timer
                            storyteller "Не успев закончить мысль, Билли замечает таймер, на котором виднеются одни лишь нули"
                            scene arasaka_explosion
                            storyteller "Арасака взрывается"
                            storyteller "Конец..."
                            return
                        "Перенаправить ракету на здание в котором сейчас нахожусь":
                            billy "Раз я тебя породил, так и умрём мы вместе"
                            scene building_explosion
                            hide billy
                            storyteller "Здание взрывается"
                            storyteller "Конец..."
                            return
                        "Согласиться на план Вектора":
                            hide billy
                            storyteller "Вектор берёт над Билли верх, закрывая и ограждая его от физического тела. Личность Билли будто складывается в нижний ящик с детскими игрушками. Прежде чем замолкнуть на долгое время, Билли задаёт вопрос антагонисту:"
                            show billy at left
                            billy "Почему ты сразу не направил ракету на дом нашего отца"
                            hide billy
                            show vector at right
                            vector "Я хотел, чтобы мы сделали это вместе и ты понял, кто ты"
                            hide vector
                            scene mansion_explosion
                            storyteller "Вилла отца взорвалась"
                            storyteller "Конец..."
                            return


label deactivation:
    $ quiz_score = 0
    menu:
        "Что является определением воздействия (exposure) на безопасность?"
        "Нечто, приводящее к ущербу от угрозы":
            "Правильный ответ"
            $ quiz_score += 1
        "Любой недостаток или отсутствие информационной безопасности":
            "Неправильно\nПравильный ответ: Нечто, приводящее к ущербу от угрозы"
        "Потенциальные потери от угрозы":
            "Неправильно\nПравильный ответ: Нечто, приводящее к ущербу от угрозы"
    
    menu:
        "Почему при проведении анализа информационных рисков следует привлекать к этому специалистов из различных подразделений компании?"
        "Чтобы убедиться, что проводится справедливая оценка":
            "Неправильно\nПравильный ответ: Поскольку люди в различных подразделениях лучше понимают риски в своих подразделениях и смогут предоставить максимально полную и достоверную информацию для анализа"
        "Это не требуется. Для анализа рисков следует привлекать небольшую группу специалистов, не являющихся сотрудниками компании, что позволит обеспечить беспристрастный и качественный анализ":
            "Неправильно\nПравильный ответ: Поскольку люди в различных подразделениях лучше понимают риски в своих подразделениях и смогут предоставить максимально полную и достоверную информацию для анализа"
        "Поскольку люди в различных подразделениях лучше понимают риски в своих подразделениях и смогут предоставить максимально полную и достоверную информацию для анализа":
            "Правильный ответ"
            $ quiz_score += 1

    menu:
        "Защита информации это:"
        "Преобразование информации, в результате которого содержание информации становится непонятным для субъекта, не имеющего доступа":
            "Неправильно\nПравильный ответ: Деятельность по предотвращению утечки информации, несанкционированных и непреднамеренных воздействий на неё"
        "Совокупность правил, регламентирующих порядок и условия доступа субъекта к информации и ее носителям":
            "Неправильно\nПравильный ответ: Деятельность по предотвращению утечки информации, несанкционированных и непреднамеренных воздействий на неё"
        "Деятельность по предотвращению утечки информации, несанкционированных и непреднамеренных воздействий на неё":
            "Правильный ответ"
            $ quiz_score += 1
        
    if quiz_score == 3:
        scene rocket_lake
        storyteller "Ракету удалось деактивировать"
        show billy at left
        billy "Фууух, этот день запомнится мне надолго. Жаль, что я так и не узнал, кто запустил ракету. Надеюсь, начальство завтра разберётся со всей этой чертовщиной"
        hide billy
        storyteller "Конец..."
        return
    else:
        show billy at left
        billy "Нееет! Я не смог деактивировать ракету. Я поставил жизни миллионов людей под угрозу"
        hide billy
        scene arasaka_explosion
        storyteller "Арасака пала. Билли сделал всё, что было в его силах"
        storyteller "Конец"
        return


label quiz:
    play music mas_que_nada
    $ quiz_score = 0
    menu:
        "Спам распространяет поддельные сообщения от имени банков или финансовых компаний, целью которых является сбор логинов, паролей и пин-кодов пользователей:"
        "Черный пиар":
            "Неправильно\nПравильный ответ: Фишинг"
        "Фишинг":
            "Правильный ответ"
            $ quiz_score += 1
        "Нигерийские письма":
            "Неправильно\nПравильный ответ: Фишинг"
        "Пустые письма":
            "Неправильно\nПравильный ответ: Фишинг"

    menu:
        "Кто в конечном счете несет ответственность за гарантии того, что данные классифицированы и защищены?"
        "Владельцы данных":
            "Неправильно\nПравильный ответ: Руководство"
        "Пользователи":
            "Неправильно\nПравильный ответ: Руководство"
        "Администраторы":
            "Неправильно\nПравильный ответ: Руководство"
        "Руководство":
            "Правильный ответ"
            $ quiz_score += 1
    
    menu:
        "Символы шифруемого текста последовательно складываются с символами некоторой специальной последовательности, это метод:"
        "Гаммирования":
            "Правильный ответ"
            $ quiz_score += 1
        "Подстановки":
            "Неправильно\nПравильный ответ: Гаммирования"
        "Кодирования":
            "Неправильно\nПравильный ответ: Гаммирования"
        "Аналитических преобразований":
            "Неправильно\nПравильный ответ: Гаммирования"

    menu:
        "Что такое CobiT и как он относится к разработке систем информационной безопасности и программ безопасности?"
        "Список стандартов, процедур и политик для разработки программы безопасности":
            "Неправильно\nПравильный ответ: Открытый стандарт, определяющий цели контроля"
        "Текущая версия ISO 17799":
            "Неправильно\nПравильный ответ: Открытый стандарт, определяющий цели контроля"
        "Структура, которая была разработана для снижения внутреннего мошенничества в компаниях":
            "Неправильно\nПравильный ответ: Открытый стандарт, определяющий цели контроля"
        "Открытый стандарт, определяющий цели контроля":
            "Правильный ответ"
            $ quiz_score += 1
    
    menu:
        "Если различным группам пользователей с различным уровнем доступа требуется доступ к одной и той же информации, какое из указанных ниже действий следует предпринять руководству?"
        "Снизить уровень безопасности этой информации для обеспечения ее доступности и удобства использования":
            "Неправильно\nПравильный ответ: Улучшить контроль за безопасностью этой информации"
        "Требовать подписания специального разрешения каждый раз, когда человеку требуется доступ к этой информации":
            "Неправильно\nПравильный ответ: Улучшить контроль за безопасностью этой информации"
        "Улучшить контроль за безопасностью этой информации":
            "Правильный ответ"
            $ quiz_score += 1
        "Снизить уровень классификации этой информации":
            "Неправильно\nПравильный ответ: Улучшить контроль за безопасностью этой информации"

    show billy at right

    if quiz_score >= 4:
        billy "Жизнь полна боли... но есть в ней также любовь, красота и код. Неужели я закончил"
        "Поздравляю, ты правильно ответил на вопросы"    
        scene laptop
        storyteller "Билли выдыхает и закрывает ноутбук"
    else:
        billy "Ты должен быть готов к тому, что можешь быть неправ. Эххх, разберусь с этим завтра"
        "Ты неправильно ответил на вопросы....."
        scene laptop
        storyteller "Билли вздыхает и закрывает ноутбук"    

    hide billy
    stop music fadeout 1.0
    return
